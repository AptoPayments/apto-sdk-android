apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def config = rootProject.extensions.getByName("ext")
version = config["version_name"]
group = config["application_id"]

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/core-release.aar")
            groupId this.group
            artifactId artifact
            artifact sourcesJar
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    override = true
    publish = true
    pkg {
        repo = 'maven'
        name = 'com-aptopayments-sdk'
        userOrg = 'apto'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/AptoPayments/apto-sdk-android.git'
        version {
            name = this.version
            desc = "Apto Payments SDK ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}
