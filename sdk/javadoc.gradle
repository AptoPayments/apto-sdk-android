android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", 
            type: Javadoc,
            group: 'Documentation',
            dependsOn: "assemble${variant.name.capitalize()}") {

        description = "Generates Ledge Line SDK Java Documentation for ${variant.name.capitalize()}."
        title = "Ledge Line SDK Java documentation"
        source = variant.javaCompile.source

        classpath += files(variant.javaCompile.classpath.files, project.android.getBootClasspath())

        destinationDir = reporting.file("sdk-docs")
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}
