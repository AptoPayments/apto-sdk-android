apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply from: '../buildsystem/jacoco.gradle'
apply from: "../buildsystem/ktlint.gradle"
apply from: '../buildsystem/dokka.gradle'
apply from: 'dokka-config.gradle'

def config = rootProject.extensions.getByName("ext")

android {

    compileSdkVersion config["compile_sdk"]
    buildToolsVersion config["build_tools_version"]

    defaultConfig {
        minSdkVersion config["min_sdk"]
        targetSdkVersion config["target_sdk"]
        versionCode config["version_code"]
        versionName findProperty("POM_LIBRARY_VERSION")
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }

    lintOptions {
        ignoreWarnings true
        abortOnError true
        ignoreTestSources true
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled(!project.hasProperty('jacocoDisabled'))
            crunchPngs false
        }
        release {
            testCoverageEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    def applicationDependencies = rootProject.ext.mainApplication
    def unitTestDependencies = rootProject.ext.unitTesting
    def acceptanceTestDependencies = rootProject.ext.acceptanceTesting

    //Application dependencies
    implementation applicationDependencies.kotlin
    implementation applicationDependencies.kotlinCoroutines
    implementation applicationDependencies.kotlinCoroutinesAndroid
    implementation applicationDependencies.androidAnnotations
    implementation applicationDependencies.koin
    implementation applicationDependencies.koinViewModel
    implementation applicationDependencies.retrofit
    implementation applicationDependencies.okhttpLoggingInterceptor
    implementation applicationDependencies.playServices
    implementation applicationDependencies.room
    kapt applicationDependencies.roomKaptAnnotationProcessor
    implementation applicationDependencies.lifecycleExtensions
    kapt applicationDependencies.lifecycleKaptAnnotationProcessor
    implementation applicationDependencies.threeTen
    implementation applicationDependencies.androidSecurity

    //Unit/Integration tests dependencies
    testImplementation unitTestDependencies.kotlin
    testImplementation unitTestDependencies.kotlinTest
    testImplementation(unitTestDependencies.robolectric) {
        exclude group: 'com.google.auto.service', module: 'auto-service'
    }
    testImplementation unitTestDependencies.junit
    testImplementation unitTestDependencies.mockito
    testImplementation unitTestDependencies.kluent
    testImplementation unitTestDependencies.koinTest
    testImplementation unitTestDependencies.coreTesting
    testImplementation('org.threeten:threetenbp:1.4.4') {
        exclude module: applicationDependencies.threeTen
    }
    testImplementation unitTestDependencies.mockWebServer
}
